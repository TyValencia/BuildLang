BuildLang {
    Program     = Stmt+

    Stmt        = VarDecl
                | FunDecl
                | LeftPipe
                | RightPipe
                | Primary_call
                | Assignment
                | Primary ("++" | "--")                                      -- bump
                | IfStmt
                | LoopStmt
                | break                                                      -- break
                | send Exp                                                   -- return
                | send                                                       -- shortreturn

    VarDecl     = ReadOnly? type id "=" Exp
    ReadOnly    = "$"
    type        = (int | float | string | bool | void)
    FunDecl     = (async)? block id Params (sends type)? ":" Stmt*
    Params      = "(" ListOf<Param, ","> ")"
    Param       = (type | TypeArray) id
    TypeArray   = ("[")* type ("]")* 
    Assignment  = NonemptyListOf<id, ","> "=" NonemptyListOf<Exp, ",">       -- multipleAssignment
                | Primary "=" Exp                                            -- assignment
    LeftPipe    = (id "<|")+ PrimaryList 
    RightPipe   = PrimaryList ("|>" id)+ 
    PrimaryList = NonemptyListOf<Primary, ",">
    Args        = ListOf<Exp, ",">                                           -- args
    IfStmt      = if Exp ":" StmtBlock else ":" StmtBlock                    -- long
                | if Exp ":" StmtBlock else IfStmt                           -- elseif
                | if Exp ":" StmtBlock                                       -- short
    LoopStmt    = while Exp ":" StmtBlock                                    -- while
                | stack intlit ":" StmtBlock                              	 -- repeat
                | for id in intlit ("..." | "..<") intlit ":" StmtBlock      -- range
                | for id in Exp ":" StmtBlock                                -- collection

    StmtBlock   = Stmt+
    
    Exp         = Exp1 ("||" Exp1)+                                          -- or
                | Exp1 ("&&" Exp1)+                                          -- and
                | Exp1 
    Exp1        = Exp2 ("<="|"<"|"=="|"!="|">="|">") Exp2                    -- compare
                | Exp2
    Exp2        = Exp2("+" | "-") Exp3                                       -- binary
               	| Exp3
    Exp3        = Exp3 ("*" | "/" | "%") Exp4                                -- binary
                | Exp4
    Exp4        = Primary "**" Exp4                                          -- binary
                | ("-" | "!" | random | len) Primary                         -- unary
                | Primary
    Primary     = true ~mut
                | false ~mut
                | floatlit ~mut
                | intlit ~mut
                | Primary ("(" | "?(") ListOf<Exp, ","> ")" ~mut             -- call
              	| Primary ("[" | "?[") Exp "]"                               -- subscript
                | stringlit ~mut
              	| id                                                         -- id
              	| TypeArray "(" ")" ~mut                                     -- emptyarray
                | "[" NonemptyListOf<Exp, ","> "]" ~mut                      -- arrayexp
                | "(" Exp ")" ~mut                                           -- parens
    
    intlit      = digit+
    floatlit    = digit+ "." digit+ (("E" | "e") ("+" | "-")? digit+)?
    stringlit   =  "\"" char* "\""
    char        = ~control ~"\\" ~"\"" any
                | "\\" ("n" | "t" | "\"" | "\\")                             -- escape
                | "\\u{" hex hex? hex? hex? hex? hex? "}"                    -- codepoint
    control     = "\x00".."\x1f" | "\x80".."\x9f"
    hex         = hexDigit
    mut         = ~"==" "=" | "++" | "--"

    break       = "break" ~alnum
    if          = "if" ~alnum
    else       	= "else" ~alnum
    while       = "while" ~alnum
    for         = "for" ~alnum
    async       = "async" ~alnum
    random      = "random" ~alnum
    len        = "len" ~alnum
    block       = "block" ~alnum
    send        = "send" ~alnum
    sends       = "sends" ~alnum
    stack 	    = "stack" ~alnum
    in          = "in" ~alnum
    int         = "int" ~alnum
    float 	    = "float" ~alnum
    string 	    = "string" ~alnum
    bool        = "bool" ~alnum
    void        = "void" ~alnum
    true        = "true" ~alnum
    false 	    = "false" ~alnum
    keyword	    = break | if | else | while 
                | len | random | async | block | send | sends | stack | for | in 
                | int | float | string | bool | void | true | false

    indent      =  "⇨"
    dedent      =  "⇦"
    
    id          = ~keyword letter alnum*
    space      	+= "//" (~"\n" any)*                                         -- comment

}